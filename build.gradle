group 'me.yamakaja.commanditems'
version '2.4.7'

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

processResources {
    filesMatching("**/plugin.yml") {
        filter {
            it.replace('@version@', project.getVersion())
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.10.0-SNAPSHOT'

    implementation "org.bstats:bstats-bukkit:2.2.1"

    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.0"
}

shadowJar {
    archiveBaseName.set('CommandItems')

    relocate 'org.bstats', 'me.yamakaja.commanditems.lib.org.bstats'
    relocate 'co.aikar.commands', 'me.yamakaja.commanditems.lib.co.aikar.commands'
    relocate 'org.intellij', 'me.yamakaja.commanditems.lib.org.intellij'
    relocate 'org.jetbrains', 'me.yamakaja.commanditems.lib.org.jetbrains'
    relocate 'net.jodah', 'me.yamakaja.commanditems.lib.net.jodah'
    relocate 'org.yaml.snakeyaml', 'me.yamakaja.commanditems.lib.org.yaml.snakeyaml'
    relocate 'com.fasterxml', 'me.yamakaja.commanditems.lib.com.fasterxml'
}


task install(type: Copy) {
    from("$rootDir/build/libs/") {
        include "**/CommandItems-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testenv/plugins/"
}
